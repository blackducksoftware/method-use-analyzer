plugins{
    id 'maven-publish'
    id 'de.marcphilipp.nexus-publish' version "0.4.0"
}

description = 'Library which analyzes Java projects for method calls to external methods'

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    api 'com.google.guava:guava:31.1-jre'
    api 'org.ow2.asm:asm:9.6'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.slf4j:slf4j-api:1.7.36'

    testImplementation project(':method-analyzer-test-project')
    testImplementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation 'org.testng:testng:7.5'
    
    // Overrides vulnerable version 1.21 in transitive dependencies of TestNG
    testImplementation 'org.yaml:snakeyaml:1.33'
}

// TODO romeara Add to contribution doc
// Configure tests with directory for test project
// This requires setting up TestNG run/debug properties within IDE imports to execute tests
test {
    systemProperty 'com.blackducksoftware.method.analyzer.test.project.dir', "${project(':method-analyzer-test-project').projectDir}"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            
            pom {
                name = project.name
                description = project.description
                url = "https://www.github.com/blackducksoftware/${project.rootProject.name}"
                
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'blackduckoss'
                        name = 'Black Duck OSS'
                        email = 'bdsoss@blackducksoftware.com'
                        organization = 'Black Duck Software, Inc.'
                        organizationUrl = 'https://www.blackduck.com'
                        timezone = 'America/New_York'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/blackducksoftware/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:blackducksoftware/${project.rootProject.name}.git"
                    url = "https://www.github.com/blackducksoftware/${project.rootProject.name}"
                }
            }
        }
    }
}