plugins {
    id 'com.autonomousapps.dependency-analysis' version '0.46.0'
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'eclipse'

    // Always download sources, to allow debugging
    eclipse {
        classpath {
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }
    
    check.dependsOn buildHealth
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    
    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [ 'checkstyle.config.dir' : rootProject.file('config/checkstyle') ]
        toolVersion = '8.33'
    }
     
    task checkstyleAll{}
     
    tasks.withType(Checkstyle).all { checkstyleTask -> checkstyleAll.dependsOn checkstyleTask }
     
    check.dependsOn checkstyleAll
    
    // Setup default test behavior, including failure logging
    test {
        useTestNG() {
            useDefaultListeners = true
        }
    }
    
    // Set Java 9 automatic module name to prevent future collisions
    jar {
        manifest {
            attributes("Automatic-Module-Name": "${project.group}.${project.name}".replaceAll("-", "."))
        }
    }
    
    tasks.withType(Jar).all {
        from("${rootDir}"){
            include 'LICENSE'
        }
    }
    
}
