allprojects {
    apply plugin: 'eclipse'

    // Always download sources, to allow debugging
    eclipse {
        classpath {
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    
    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [ 'checkstyle.config.dir' : rootProject.file('config/checkstyle') ]
        toolVersion = '8.27'
    }
     
    task checkstyleAll{}
     
    tasks.withType(Checkstyle).all { checkstyleTask -> checkstyleAll.dependsOn checkstyleTask }
     
    check.dependsOn checkstyleAll
    
    // Setup default test behavior, including failure logging
    test {
        useTestNG() {
            useDefaultListeners = true
        }
    }
    
    // TODO module name
    
    tasks.withType(Jar).all {
        from("${rootDir}"){
            include 'LICENSE'
        }
    }
    
}
